apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

//temporary jacoco fix for java8
jacoco {
    toolVersion = "0.7.0.201403182114"
}

sourceCompatibility = 1.6
//sourceCompatibility = 1.8
version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'javasoft.ch Decimal', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.freemarker:freemarker:2.3.14'
    compile 'org.apache.commons:commons-io:1.3.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.openjdk.jmh:jmh-core:1.1.1'
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

buildscript {
	repositories {
		mavenCentral()
	}
	
	dependencies {
		classpath group: 'net.sourceforge.fmpp', name: 'fmpp', version: '0.9.14'
	}
	
	ant.taskdef(name: 'fmpp', classname:'fmpp.tools.AntTask', classpath: buildscript.configurations.classpath.asPath)
}

def generatedSources = "${buildDir}/generated-src"

task generateSources() << {
	println "Generating sources...."
	ant.fmpp(
		configuration: "src/main/resources/codegen/config.fmpp",
		sourceRoot: "src/main/resources/codegen/templates",
		outputRoot: generatedSources
	)
}
	
compileJava.dependsOn generateSources

sourceSets.main.java {
	srcDirs generatedSources, 'src/main/java'
}
