apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'me.champeau.gradle.jmh'

//temporary jacoco fix for java8
jacoco {
    toolVersion = "0.7.0.201403182114"
}

sourceCompatibility = 1.6
//sourceCompatibility = 1.8
version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'javasoft.ch Decimal', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.freemarker:freemarker:2.3.14'
    compile 'org.apache.commons:commons-io:1.3.2'
    testCompile 'junit:junit:4.11'
    jmh 'org.openjdk.jmh:jmh-core:1.1.1'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.1.1'
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	
	dependencies {
		classpath 'net.sourceforge.fmpp:fmpp:0.9.14'
		classpath 'me.champeau.gradle:jmh-gradle-plugin:0.1.3'
	}
	
	ant.taskdef(name: 'fmpp', classname:'fmpp.tools.AntTask', classpath: buildscript.configurations.classpath.asPath)
}

def generatedSources = "${buildDir}/generated-src"

task generateSources() << {
	println "Generating sources...."
	ant.fmpp(
		configuration: "src/main/resources/codegen/config.fmpp",
		sourceRoot: "src/main/resources/codegen/templates",
		outputRoot: generatedSources
	)
}
	
compileJava.dependsOn generateSources

sourceSets.main.java {
	srcDirs generatedSources, 'src/main/java'
}
    
jmh {
   include = '.*Benchmark.*'
   iterations = 3 // Number of measurement iterations to do.
   warmupIterations = 3
   benchmarkMode = 'all' // Benchmark mode. Available modes are: [Throughput/thrpt, AverageTime/avgt, SampleTime/sample, SingleShotTime/ss, All/all]
   batchSize = 1 // Batch size: number of benchmark method calls per operation. (some benchmark modes can ignore this setting)
   fork = 2 // How many times to forks a single benchmark. Use 0 to disable forking altogether
   failOnError = false // Should JMH fail immediately if any benchmark had experienced the unrecoverable error?
}
